# Deviation for each User Avg cred value of shared news article vs User cred 

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX base: <https://nobias-project.eu/>
SELECT ?UserID (SAMPLE(?value) as ?UserCred) (Count(?UserID) AS ?SharedNews) (Avg(1-xsd:float(?news_value)) AS ?avgCred) ((abs(xsd:float(?UserCred)-?avgCred)) AS ?deviation)
WHERE {
# Get all RVAs Usercredlat(U)
?rva_instance rdf:type <https://nobias-project.eu/RandomVariableAtom> ;
   <https://nobias-project.eu/rva_name> ?name ;
   <https://nobias-project.eu/hasDataset> <https://nobias-project.eu/Dataset/Data1> ;
   <https://nobias-project.eu/value> ?value .

FILTER regex(?name, "USERCREDLAT", "i") .
# Extracting user argument from RVA + UserCred
?rand_inst_arg <https://nobias-project.eu/hasAtom> ?rva_instance .
?rva_instance <https://nobias-project.eu/value> ?user_cred .
?rand_inst_arg <https://nobias-project.eu/hasArgument> ?user_arg .
?obs_inst_arg <https://nobias-project.eu/hasArgument> ?user_arg .
?user_arg <https://nobias-project.eu/inst_arg_name> ?UserID .
# Getting News argument from ObservedAtom UserShare(N,U)
?obs_inst_arg rdf:type <https://nobias-project.eu/ObsvAtomInstArgument> .
?obs_inst_arg <https://nobias-project.eu/hasAtom> ?obs_atom .
?obs_inst_arg2 <https://nobias-project.eu/hasAtom> ?obs_atom .
?obs_inst_arg2 <https://nobias-project.eu/position> "1" .
?obs_inst_arg2 <https://nobias-project.eu/hasArgument> ?news_arg_instance .
# Getting Truth value of News through Fakenews(N)
?inst_arg <https://nobias-project.eu/hasAtom> ?news_atom .
?inst_arg <https://nobias-project.eu/hasArgument> ?news_arg_instance .
?news_atom <https://nobias-project.eu/obsatom_name> ?news_atom_name .
FILTER regex(?news_atom_name, "FAKENEWS", "i") .
?news_atom <https://nobias-project.eu/value> ?news_value .
}
GROUP BY ?UserID
